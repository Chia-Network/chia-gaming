
(include *standard-cl-23*)

(import onchain.poker.spacehandcalc exposing space_hand_calc)
(import std.assert)

; state is (minraise half_pot raise_amount mover_image_N waiter_image_N begin_round_hash)
; move is concatenation of mover_image_N and raise amount
; evidence is empty
(export (quine_hash (move new_validation_hash max_move_size mover_share previous_validation_hash 
    mover_puzzle_hash waiter_puzzle_hash amount timeout max_move_size referee_hash) 
    (half_pot_size mover_image waiter_preimage) me mover_puzzle solution evidence)

    (if_any_fail
        (= (strlen move) 33)
        (! new_validation_hash)
        (= mover_image (sha256 (substr move 0 32)))
        0
        (assign
            (mover_cards waiter_cards mover_boost waiter_boost) (make_all_cards (substr move 0 32) waiter_preimage)
            mover_selected_cards (pull_from_bitfield mover_cards (substr move 32 33))
            waiter_selected_cards (pull_from_bitfield waiter_cards evidence)
            (if (!= (len mover_selected_cards) 5)
                0
                (assert (= (len waiter_selected_cards) 5)
                    (!= mover_share
                        (assign result (deep_compare (space_hand_calc mover_selected_cards mover_boost) 
                                (space_hand_calc waiter_selected_cards waiter_boost))
                            (if not result
                                (/ amount 2)
                                (= result 1)
                                (- (/ amount 2) half_pot_size)
                                (+ (/ amount 2) half_pot_size)
                            )
                        )
                    )
                )
            )
        )
    )
)

(defun pull_from_bitfield (cards bitfield)
    (if (not cards) 0
        (assign remaining (pull_from_bitfield (r cards) (logrshift bitfield 1))
            (if (& bitfield 1)
                (c (f cards) remaining)
                remaining
            )
        )
    )
)

(defun make_all_cards (alice_pre bob_pre)
    (assign
        alice_image_1 (sha256 alice_pre)
        alice_image_2 (sha256 alice_image_1)
        alice_image_3 (sha256 alice_image_2)
        alice_image_4 (sha256 alice_image_3)
        bob_image_1 (sha256 bob_pre)
        bob_image_2 (sha256 bob_image_1)
        bob_image_3 (sha256 bob_image_2)
        bob_image_4 (sha256 bob_image_3)
        river_card (make_cards alice_image_3 bob_image_3 1)
        turn_card (make_cards alice_image_2 bob_image_2 1)
        flop_cards (make_cards alice_image_1 bob_image_1 3)
        (alice_hole_1 alice_hole_2) (make_cards alice_pre bob_image_4)
        (bob_hole_1 bob_hole_2) (make_cards bob_pre alice_image_4)
        alice_cards (li alice_hole_1 alice_hole_2 river_card turn_card &rest flop_cards)
        bob_cards (li bob_hole_1 bob_hole_2 river_card turn_card &rest flop_cards)
        alice_boost (not (% (sha256 (logxor alice_pre bob_image_4)) 3))
        bob_boost (not (% (sha256 (logxor bob_pre alice_image_4)) 3))
        (li alice_cards bob_cards alice_boost bob_boost)
    )
)

(defun make_cards (thing1 thing2 num_cards)
    (extract_cards (sha256 (logxor thing1 thing2)) num_cards)
)

(defun extract_cards (value num_cards)
    (if (not num_cards)
        0
        (assign 
            (nextval next_card) (divmod value 13)
            (c (+ 2 next_card) (extract_cards nextval (- numcards 1)))
        )
    )
)