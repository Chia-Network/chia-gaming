(include *standard-cl-23*)

(import std.or)
(import std.and)
(import std.shatree)
(import std.curry)
(import std.print)
(import games.calpoker-v0.game_codes)

(defmac STATE_PACK ()
  (q @ state-pack
     (MOVER_PUZZLE_HASH
      WAITER_PUZZLE_HASH
      TIMEOUT
      AMOUNT
      MOD_HASH
      NONCE
      MOVE
      MAX_MOVE_SIZE
      VALIDATION_INFO_HASH
      MOVER_SHARE
      PREVIOUS_VALIDATION_INFO_HASH
      )
     )
  )

(defmac CURRY_PACK () (q @ curry-args (COUNT SELF_HASH SELF_PROG MOVER0 WAITER0)))

(defun curry-args-incr ((CURRY_PACK))
  (c (+ 1 COUNT) (r curry-args))
  )

(defun validator-hash ((CURRY_PACK))
  (curry_hashes_inline
   SELF_HASH
   (shatree "validator")
   (shatree curry-args)
   )
  )

;; Every valid move encapsulates the entire set of data that the validator will be exposed
;; to.
;;
;; MOVE is the concatenation of all curried parameters except move followed by
;; - the shatree of previous validation program
;; - slashable
;;   a byte that is nonzero if the move is slashable.  the user must match this byte to slash.
;; - mover share
;;   the next mover share to return
(defun validator
    ((CURRY_PACK)
     mod_hash
     (STATE_PACK)
     state
     this_validation_program
     mover_puzzle
     solution
     evidence
     )
  (assign
   pv_hash (shatree (print "doing validation" this_validation_program))
   move-counter-data
   (concat
    ;; Hashes first
    MOVER_PUZZLE_HASH
    WAITER_PUZZLE_HASH
    MOD_HASH
    VALIDATION_INFO_HASH
    PREVIOUS_VALIDATION_INFO_HASH
    pv_hash
    ;; Then variable length stuff
    TIMEOUT
    AMOUNT
    NONCE
    MAX_MOVE_SIZE
    MOVER_SHARE
    COUNT
    )

   move-counter-data-len (strlen move-counter-data)
   mover-puzzle-hash (substr move-counter-data 0 32)
   move-len (strlen MOVE)

   (if
    (or
     ;; Too short move
     (print (list "have 2 or more bytes than move-counter-data-len which is" move-counter-data-len "have" (strlen MOVE)) (> move-counter-data-len (- (strlen MOVE) 2)))
     ;; Move doesn't repeat the counter data
     (print (list "did we reproduce the data" MOVE "versus" move-counter-data) (not (= move-counter-data (substr MOVE 0 move-counter-data-len))))
     ;; Mover puzzle isn't the puzzle of the mover we think we have.
     (print "did we have the right mover puzzle" (not (= mover-puzzle-hash (shatree mover_puzzle))))
     ;; check previous validation info hash
     (print "did we have the right previous validation info hash" (not (= PREVIOUS_VALIDATION_INFO_HASH (sha256 pv_hash (shatree state)))))
     ;; the step we're on indicates mover0 or waiter0 as mover
     (print "did we send mover and waiter puzzle hash in the right order" (if (logand COUNT 1)
          (not (= MOVER_PUZZLE_HASH MOVER0))
          (not (= MOVER_PUZZLE_HASH WAITER0))
          ))
     ;; if mover and waiter are the same then things are broken
     (print "ensure we didn't just send the same puzzle hash for mover and waiter" (= MOVER_PUZZLE_HASH WAITER_PUZZLE_HASH))
     ;; the evidence is the slash
     (and
      (print "evidence was the same as the cheat in the move" (= evidence (substr MOVE (- move-len 1) move-len)))
      ;; Can't slash if the slash-byte is zero.
      (print "and it wasn't a zero byte which we can't slash" (not (= evidence 0x00)))
      )
     )
    (list SLASH () "debug game: slash")
    (assign
     slashable-move (substr MOVE (- move-len 1) move-len)
     mover-share (+ 0 (substr MOVE move-counter-data-len (- move-len 1)))
     (list
      MAKE_MOVE
      (validator-hash curry-args)
      (list COUNT slashable-move mover-share)
      512
      (list "debug game: move with count" (+ COUNT 1) "slash hint" slashable-move "mover share" mover-share)
      )
     )
    )
   )
  )

;; Make a value be at least one byte.
(defun byteify (n) (i n n 0x00))

(defun myturn ((CURRY_PACK) local_move amount split entropy)
  ;; local move is a list of slashable and mover share
  (if (not local_move)
      ;; () local move should make us emit a local slash so we can try that.
      (x "my turn local slash")
      ;; local move is a list of a prefix count slash byte and mover share
      (assign
       (prefix count slash mover-share) local_move
       (if (not (= count COUNT))
           (x "count not equal for this move" count COUNT)
           (list
            "debug game: move"
            ;; move bytes
            (concat prefix (byteify count) (byteify mover-share) slash)
            (curry SELF_PROG "validator" COUNT SELF_HASH SELF_PROG)
            (validator-hash curry-args)
            (curry SELF_PROG "validator" (+ COUNT 1) SELF_HASH SELF_PROG)
            (validator-hash (curry-args-incr curry-args))
            512
            mover-share
            (curry SELF_PROG "theirturn" (curry-args-incr curry-args))
            )
           )
       )
      )
  )

(defun read-over-integer (s o p)
  (assign
   this-byte (substr s p (+ 1 p))
   ;; We're only looking for positive numbers
   (if (logand 0x80 this-byte)
       (read-over-integer s (concat this-byte o) (- p 1))
       (list (concat this-byte o) p)
       )
   )
  )

(defun theirturn
    ((CURRY_PACK)
     amount
     (@ state (count smove mshare))
     move
     validation_program_hash
     split
     )
  (assign
   move-len (strlen move)
   (if (< move-len 1)
       (list SLASH ())
       ;; Skipping the last byte and the mover share, there should be a count.
       ;; It's fairly simple to read over a positive number.
       (assign
        slash-byte (substr move (- move-len 1) move-len)
        (mover-share count-pos) (read-over-integer move "" (- move-len 1))
        (move-count _) (read-over-integer move "" (- count-pos 1))
        (if (not (= move-count COUNT))
            (x "their turn: mismatched count" move-count COUNT)
            (not (= (validator-hash curry-args) validation_program_hash))
            (x "their turn: mismatched validator program")
            (not (= split mover-share))
            (x "their turn: wrong mover share" mover-share split)
            (not (= split mshare))
            (x "their turn: wrong mover share in state" mshare split)
            (not (= smove slash-byte))
            (x "their turn: wrong slash byte in state" smove slash-byte)
            (list
             MAKE_MOVE
             (list count smove mshare)
             (list slash-byte)
             (curry SELF_PROG "myturn" (curry-args-incr curry-args))
             )
            )
        )
       )
   )
  )

(defun factory ((CURRY_PACK) i_am_initiator my_contribution their_contribution params)
  (assign
   amount (+ my_contribution their_contribution)

   (if i_am_initiator
       (list
        ;; One game
        (list
         ;; amount
         amount

         ;; my turn
         1

         ;; handler
         (curry SELF_PROG "myturn" curry-args)

         my_contribution
         their_contribution

         ;; Initial validation program
         (curry SELF_PROG "validator" curry-args)

         (validator-hash curry-args)

         ;; Initial state: count slash mover-share
         (list () () ())

         ;; Initial move
         ()

         ;; Initial max move size
         512

         ;; Initial mover share
         0
         )
        )

       (list
        ;; One game
        (list
         ;; amount
         amount

         ;; their turn
         0

         ;; handler
         (curry SELF_PROG "theirturn" curry-args)

         my_contribution
         their_contribution

         ;; Initial validation program
         (curry SELF_PROG "validator" curry-args)

         (validator-hash curry-args)

         ;; Initial state: count slash mover-share
         (list () () ())

         ;; Initial move
         ()

         ;; Initial max move size
         512

         ;; Initail mover share
         0
         )
        )
       )
   )
  )

(export (MODE (CURRY_PACK) . args)
  (if (= MODE "validator")
      (validator curry-args &rest args)
      (= MODE "myturn")
      (myturn curry-args &rest args)
      (= MODE "theirturn")
      (theirturn curry-args &rest args)
      (= MODE "factory")
      (factory curry-args &rest args)
      (x "no such personality" MODE)
      )
  )
