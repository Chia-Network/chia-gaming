(include *standard-cl-23*)

(import games.calpoker-v1.onchain.onehandcalc)
(import games.calpoker-v1.onchain.arrange_cards)
(import games.calpoker-v1.onchain.onehandcalc exposing mod52_to_pair)
(import games.calpoker-v1.game_codes)
(import std.list_compare)
(import std.assert)
(import std.relops)
(import std.popcount)
(import std.if_any_fail)
(import std.and)
(import std.or)
(import std.map)
(import std.li)
(import std.append)
(import std.print)

; Bob challenging
; state is (bob_discards alice_cards bob_cards alice_commit)
; move is alice_salted_discards and alice's selections
; evidence is Bob's card selections

(defun indices_to_bitfield_inner (mylist index)
    (if (not mylist)
        0
        (= (f mylist) index)
        (logior 1 (lsh (indices_to_bitfield_inner (r mylist) (+ index 1)) 1))
        (lsh (indices_to_bitfield_inner mylist (+ index 1)) 1)
    )
)

(defun single-byte (raw-indices)
    (if raw-indices
        (assign
            indices-len (strlen raw-indices)
            (substr raw-indices (- indices-len 1) indices-len)
        )
        0x00
    )
)

(defun indices_to_bitfield (mylist)
    (single-byte (indices_to_bitfield_inner mylist 0))
)

(defun does_alice_claim_small_enough (alice_hand_value bob_card_selections bob_final_cards mover_share amount)
    (assign
        bob_hand_value
            (onehandcalc
                (map
                    mod52_to_pair
                    (pull_out_cards bob_card_selections bob_final_cards)
                )
            )
        result (list_compare alice_hand_value bob_hand_value)
        (>= mover_share
            (if (not result)
                (/ amount 2)
                (if (= result 1)
                    0
                    amount
                )
            )
        )
    )
)

(export (mod_hash
    (MOVER_PUZZLE_HASH WAITER_PUZZLE_HASH TIMEOUT AMOUNT MOD_HASH NONCE
        MOVE MAX_MOVE_SIZE VALIDATION_INFO_HASH MOVER_SHARE PREVIOUS_VALIDATION_INFO_HASH)
        (bob_discards alice_cards bob_cards alice_commit) previous_validation_program mover_puzzle solution bob_card_selections)
    (if (or
            (not (= (strlen MOVE) 18))
            ;; Common subexpression: (substr MOVE 16 17) == alice_discards
            (not (= (popcount (substr MOVE 16 17)) 4))
            ;; Common subexpression: (substr MOVE 16 17) == alice_card_selections
            (not (= (popcount (substr MOVE 17 18)) 5))
        )
        (list SLASH () "E slash invalid final move shape (need len=18, discards_popcount=4, selects_popcount=5)" MOVE)

        (assign
            alice_salted_discards (substr MOVE 0 17)
            alice_discards (substr MOVE 16 17)
            alice_card_selections (substr MOVE 17 18)

            (alice_final_cards bob_final_cards) (get_final_cards_in_canonical_order
                alice_cards alice_discards bob_cards bob_discards)
            alice_hand_value (onehandcalc (map mod52_to_pair (pull_out_cards
                alice_card_selections alice_final_cards)))
            (if_any_fail
                (= (sha256 alice_salted_discards) alice_commit)
                (if (not bob_card_selections)
                    ;; nil evidence is only valid for off-chain local validation
                    (if WAITER_PUZZLE_HASH (x "On-chain, nil evidence") 1)
                    (does_alice_claim_small_enough
                        alice_hand_value
                        bob_card_selections
                        bob_final_cards
                        MOVER_SHARE
                        AMOUNT
                    )
                )
                (list SLASH () "E slash")

                (list MAKE_MOVE 0
                    (li
                        bob_discards
                        alice_cards
                        bob_cards
                        alice_final_cards
                        bob_final_cards
                        alice_hand_value
                    )
                    0
                    "E move"
                )
            )
        )
    )
)
