(import onchain.calpoker.make_card)
(import std.assert)
(import std.popcount)
(import std.map)
(import std.append)
(import std.sort)
(import std.deep_compare)

(defun split_cards (mask cards)
    (split_cards_inner mask cards 0 0)
)

; Use mask to determine which cards are prepended to leftcards and which to rightcards
(defun split_cards_inner (mask cards leftcards rightcards)
    (if (not cards)
        (list leftcards rightcards)
        (if (logand mask 1)
            (split_cards_inner (lsh mask -1) (r cards) leftcards (c (f cards) rightcards))
            (split_cards_inner (lsh mask -1) (r cards) (c (f cards) leftcards) rightcards)
        )
    )
)

(defun pull_out_cards_inner (selections cards)
    (if (not cards)
        0
        (if (logand selections 1)
            (c (make_card (f cards)) (pull_out_cards_inner (lsh selections -1) (r cards)))
            (pull_out_cards_inner (lsh selections -1) (r cards))
        )
    )
)

(defun pull_out_cards args
    ;; TODO Remove this check and thrown exception as code above also checks.
    (if (= (popcount (f args)) 5)
        (pull_out_cards_inner &rest args)
        (x "pull_out_cards got bad data:" args)
    )
)

(defun get_final_cards_in_canonical_order (alice_cards alice_picks bob_cards bob_picks)
    (assign
        (my_cards_me my_cards_bob) (split_cards alice_picks alice_cards)
        (bob_cards_bob bob_cards_me) (split_cards bob_picks bob_cards)
        my_all_cards (sort deep< (append my_cards_me bob_cards_me))
        bob_all_cards (sort deep< (append bob_cards_bob my_cards_bob))
        (list my_all_cards bob_all_cards)
    )
)
