(mod ((VALIDATION_PROGRAM_HASH LAST_MOVE SPLIT AMOUNT TIMEOUT MAX_MOVE_SIZE 
        MOVER_PUZZLE_HASH WAITER_PUZZLE_HASH MOD_HASH) action . args)

    (include *standard-cl-21*)
    (include assert.clinc)
    (include curry.clinc)
    (include shatree.clinc)
    (include condition_codes.clinc)
    (defconstant move 0)
    (defconstant accuse 1)
    (defconstant timeout 2)
    (compile-file refaccuse "referee_accuse.clsp")
    (defconst accusehash (shatree refaccuse))
    (defun reduce (fun lst init)
        (if lst
            (reduce fun (r lst) (a fun (list (f lst) init)))
            init
        )
    )
    (defun <= (A B) (not (> A B)))
    (defun >= (A B) (not (> B A)))
    (defun print (A B)
      (f (c B A))
      )
    (defmacro prefix_list ARGS
        (defun compile-list (args)
            (if args
                (if (r args)
                    ;; We have at least 2 things left... recurse once.
                    (qq (c (unquote (f args)) (unquote (compile-list (r args)))))
                    ;; This is the last item, so we return it whole (improper list form).
                    (qq (unquote (f args)))
                )
                0
            )
        )
        (compile-list ARGS)
    )

    (if (= action timeout)
        (reduce
            (lambda ((hash size) remaining)
                (if size
                    (c (list CREATE_COIN hash size) remaining)
                    remaining
                )
            )
            (list 
                (list MOVER_PUZZLE_HASH SPLIT)
                (list WAITER_PUZZLE_HASH (- AMOUNT SPLIT))
            )
            (list 
                (list ASSERT_SECONDS_RELATIVE TIMEOUT)
                (list CREATE_COIN_ANNOUNCEMENT 0)
            )
        )
        (if action
            ; accuse
            (assign 
                (grandparent_id parent_validation_puzzle_hash parent_everything_else_hash 
                        mover_puzzle_reveal solution) args
                new_puzzle_hash (curry_hashes accusehash (shatree (list 
                        parent_validation_puzzle_hash VALIDATION_PROGRAM_HASH LAST_MOVE SPLIT AMOUNT 
                        TIMEOUT WAITER_PUZZLE_HASH MOVER_PUZZLE_HASH)))
                conditions (a mover_puzzle_reveal solution)
                (assert
                    (print 4401 (= (print 4491 MOVER_PUZZLE_HASH) (print 4492 (shatree mover_puzzle_reveal))))
                    (print 4402 (reduce
                        (lambda ((& new_puzzle_hash AMOUNT) (@ condition (condname arg1 arg2)) agg)
                            (if agg 
                                1 
                                (if (print 4405 (= condname CREATE_COIN))
                                    (logand (print 4403 (= arg1 new_puzzle_hash)) (print 4404 (= arg2 AMOUNT)))
                                    0
                                )
                            )
                        )
                        conditions
                        0
                    ))
                    (prefix_list
                        (list ASSERT_MY_PARENT_ID 
                            (sha256
                                grandparent_id
                                (curry_hashes MOD_HASH
                                    (sha256 2 (sha256 1 parent_validation_puzzle_hash) parent_everything_else_hash)
                                )
                                AMOUNT
                            )
                        )
                        (list CREATE_COIN_ANNOUNCEMENT 0)
                        conditions
                    )
                )
            )
            ; move
            (assign 
                (new_move new_split new_validation_program_hash mover_puzzle_reveal solution) args
                new_puzzle_hash (curry_hashes MOD_HASH (shatree (list new_validation_program_hash new_move new_split
                        AMOUNT TIMEOUT MAX_MOVE_SIZE WAITER_PUZZLE_HASH MOVER_PUZZLE_HASH MOD_HASH)))
                conditions (a mover_puzzle_reveal solution)
                (assert
                    (print 3323 (<= (strlen new_move) MAX_MOVE_SIZE))
                    (print 3324 (<= new_split AMOUNT))
                    (print 3325 (>= new_split 0))
                    (print 3326 (= 32 (strlen new_validation_program_hash)))
                    (print 3327 (= MOVER_PUZZLE_HASH (shatree mover_puzzle_reveal)))
                    (print 3328 (reduce
                        (lambda ((& new_puzzle_hash AMOUNT) (@ condition (condname arg1 arg2)) agg)
                            (if agg 
                                1 
                                (if (= condname CREATE_COIN)
                                    (logand (print 3329 (= arg1 new_puzzle_hash)) (print 3330 (= arg2 AMOUNT)))
                                    0
                                )
                            )
                        )
                        (print 3322 conditions)
                        0
                    ))
                    conditions
                )
            )
        )
    )
)
